###
# Do not add configuration for things that are common to both bash and zsh
###

# Debug zsh startup. Uncomment to enable
#ZSHDEBUG=on
[ ${ZSHDEBUG} ] && zmodload zsh/zprof

: ${ZSH_DISABLE_COMPFIX:=true}

# Path to your oh-my-zsh configuration.
ZSH=$HOME/.oh-my-zsh

# Set name of the theme to load.
# Look in ~/.oh-my-zsh/themes/
ZSH_THEME="powerlevel10k/powerlevel10k"
# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ -f ~/.p10k.zsh ]] && source ~/.p10k.zsh

# P10K Customization overrides
typeset -g POWERLEVEL9K_KUBECONTEXT_DEFAULT_CONTENT_EXPANSION='${P9K_KUBECONTEXT_NAMESPACE}<-->${P9K_KUBECONTEXT_CLOUD_CLUSTER:-${P9K_KUBECONTEXT_NAME}}@${P9K_KUBECONTEXT_CLUSTER}'
export POWERLEVEL9K_CONTEXT_TEMPLATE="%n@%m"
# export POWERLEVEL9K_TRANSIENT_PROMPT=off
export POWERLEVEL9K_KUBECONTEXT_SHOW_ON_COMMAND='kubectl|helm|kubens|kubectx|oc|istioctl'
# p10k-on-post-prompt() { p10k display '1'=hide '2/right/time'=show }
# p10k-on-pre-prompt()  { p10k display '1'=show '2/right/time'=hide }


# Automatically quote globs in URL and remote references
__remote_commands=(scp rsync)
autoload -U url-quote-magic
zle -N self-insert url-quote-magic
zstyle -e :urlglobber url-other-schema '[[ $__remote_commands[(i)$words[1]] -le ${#__remote_commands} ]] && reply=("*") || reply=(http https ftp)'

# Disable zsh globbing (using wildcards in commands)
unsetopt nomatch

# Uncomment the following line to display red dots whilst waiting for completion.
COMPLETION_WAITING_DOTS="true"

# Autosuggest Async                                                                      â”‚
ZSH_AUTOSUGGEST_USE_ASYNC=true

# Custom plugins may be added to ~/.oh-my-zsh/custom/plugins/
# Syntax highlight should be loaded before history-substring-search
plugins=(
    git
    macos
    colorize
    python
    brew
    extract
    history-substring-search
    autojump
    sudo
    vscode
    zsh-iterm-touchbar
    docker
    tmux
    golang
    fast-syntax-highlighting
    history-substring-search
    zsh-autosuggestions
    systemd
    dirhistory
    history
    common-aliases
    tig
    ripgrep
    zsh-completions
    fzf-tab
    fzf
    screen
)

source $ZSH/oh-my-zsh.sh

# Enable zsh plugins
source $ZSH_CUSTOM/plugins/autoupdate-oh-my-zsh-plugins/autoupdate.plugin.zsh
source $ZSH_CUSTOM/plugins/zsh-you-should-use/you-should-use.plugin.zsh
source $ZSH_CUSTOM/plugins/forgit/forgit.plugin.zsh

# Configure completions
fpath=($ZSH_CUSTOM/plugins/zsh-completions/src $fpath)
fpath=(/usr/local/share/zsh-completions $fpath)
fpath=($HOME/.dotfiles/completion $fpath)

# Only show targets on Makefile completion
zstyle ':completion:*:*:make:*' tag-order 'targets'

# zmv
autoload -U zmv

# Disable zsh-autocompletion on paste
pasteinit() {
  OLD_SELF_INSERT=${${(s.:.)widgets[self-insert]}[2,3]}
  zle -N self-insert url-quote-magic # I wonder if you'd need `.url-quote-magic`?
}

pastefinish() {
  zle -N self-insert $OLD_SELF_INSERT
}
zstyle :bracketed-paste-magic paste-init pasteinit
zstyle :bracketed-paste-magic paste-finish pastefinish

# Key binds
autoload -U history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
# bindkey "^[[A" history-beginning-search-backward-end
# bindkey "^[[B" history-beginning-search-forward-end
# For iTerm2
bindkey "^[^[[C" forward-word
bindkey "^[^[[D" backward-word
# For Terminal
bindkey "^[f" forward-word
bindkey "^[b" backward-word

bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down
export HISTORY_SUBSTRING_SEARCH_FUZZY=true
setopt appendhistory
setopt SHARE_HISTORY

# Initialize completions
autoload -Uz compinit
setopt EXTENDEDGLOB
for dump in $ZSH_COMPDUMP(#qN.m1); do
  compinit
  if [[ -s "$dump" && (! -s "$dump.zwc" || "$dump" -nt "$dump.zwc") ]]; then
    zcompile "$dump"
  fi
  #echo "Initializing Completions..."
done
unsetopt EXTENDEDGLOB
compinit -C

# Make pushd/popd silent
setopt PUSHDSILENT

# Fuzzy matching of completions for when you mistype them:
zstyle ':completion:*' completer _complete _match _approximate
zstyle ':completion:*:match:*' original only
zstyle ':completion:*:approximate:*' max-errors 1 numeric
zstyle -e ':completion:*:approximate:*' max-errors 'reply=($((($#PREFIX+$#SUFFIX)/3))numeric)'

# Load additional shell rc common to zsh and bash
source ~/.dotfiles/shellconfig/shellrc.sh

# Debug zsh startup. Check variable on top.
[ ${ZSHDEBUG} ] && zprof

